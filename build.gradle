import groovy.json.JsonSlurper

plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT' apply false
}

subprojects {
	apply plugin: "fabric-loom"

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	archivesBaseName = rootProject.archives_base_name
	version = rootProject.mod_version
	group = rootProject.maven_group

	ext.modName = new JsonSlurper().parse(file("src/main/resources/fabric.mod.json")).name

	repositories {
		// Add repositories to retrieve artifacts from in here.
		// You should only use this when depending on other mods because
		// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
		// See https://docs.gradle.org/current/userguide/declaring_repositories.html
		// for more information about repositories.
		maven {
			url "https://maven.terraformersmc.com/"
		}
	}

	dependencies {
		// To change the versions see the gradle.properties file
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings loom.officialMojangMappings()
		modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"

		// Fabric API. This is technically optional, but you probably want it anyway.
		modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

		modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"

		// Using modApi because api does not include dependency inside Maven pom file
		// see https://github.com/FabricMC/fabric-loom/issues/200
		modApi "org.spongepowered:configurate-hocon:${rootProject.configurate_version}"

	}

	processResources {
		inputs.property "version", project.version

		filesMatching("fabric.mod.json") {
			expand "version": project.version
		}
	}

	tasks.withType(JavaCompile).configureEach {
		// ensure that the encoding is set to UTF-8, no matter what the system default is
		// this fixes some edge cases with special characters not displaying correctly
		// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
		it.options.encoding = "UTF-8"

		// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
		it.options.release = 17
	}
}